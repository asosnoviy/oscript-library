
#Использовать json
#Использовать logos

Перем мНастройки;
Перем мПутьФайлаНастроек;
Перем Лог;

Процедура УстановитьФайлНастроек(Знач Путь) Экспорт

	мПутьФайлаНастроек = Путь;
	мНастройки = Неопределено;

КонецПроцедуры

Функция Получить() Экспорт

	Если мНастройки = Неопределено Тогда
		Попытка
			ПрочитатьФайлНастроек();
		Исключение
			Лог.Отладка("Чтение файла настроек:
				|" + ОписаниеОшибки());
			УстановитьНастройкиПоУмолчанию();
		КонецПопытки;
	КонецЕсли;

	Возврат мНастройки;

КонецФункции

Процедура ПрочитатьФайлНастроек()
	
	Если Не ЗначениеЗаполнено(мПутьФайлаНастроек) Тогда
		ВызватьИсключение "Не установлен файл настроек";
	КонецЕсли;

	Текст = ПрочитатьФайл(мПутьФайлаНастроек);

	Чтение = Новый ПарсерJSON;
	Настройки = Чтение.ПрочитатьJSON(Текст);

	// TODO сделать конвертацию терминов json в русские свойства настроек

	мНастройки = Настройки;

КонецПроцедуры

Функция ПрочитатьФайл(Знач Путь)

	Чтение = Новый ЧтениеТекста(Путь);
	Текст = Чтение.Прочитать();
	Чтение.Закрыть();

	Возврат Текст;

КонецФункции

Процедура СохранитьФайл(Знач Текст,Знач Путь)

	Запись = Новый ЗаписьТекста(Путь);
	Запись.ЗаписатьСтроку(Текст);
	Запись.Закрыть();

КонецПроцедуры

Процедура УстановитьНастройкиПоУмолчанию()
	мНастройки = Новый Структура;
	мНастройки.Вставить("ИспользоватьПрокси", Ложь);
КонецПроцедуры

Процедура СохранитьНастройки(Знач Параметры) Экспорт
	СтруктураНастроек =  Новый Структура();
	СтруктураПрокси = ЗаполнитьСтруктуруПрокси(Параметры);
	СтруктураНастроек.Вставить("Прокси",СтруктураПрокси);
	Текст = СформироватьТекстНастроек(СтруктураНастроек);
	СохранитьФайл(Текст,мПутьФайлаНастроек);
КонецПроцедуры

Функция СформироватьТекстНастроек(Знач СтруктураНастроек)
	ТекстНастроек = "";
	
	Json =  Новый ПарсерJSON;
	ТекстНастроек = Json.ЗаписатьJSON(СтруктураНастроек);

	Возврат ТекстНастроек;
КонецФункции

Функция ЗаполнитьСтруктуруПрокси(знач ЗначенияПараметров)
	СтруктураПрокси = Новый Структура("ИспользоватьПрокси,ИспользоватьАутификациюОС,Сервер,Порт,Пользователь,Пароль",Ложь,Ложь);
	//СтруктураПрокси = Новый Структура;
	СтруктураПрокси.Вставить("ИспользоватьПрокси",?(ЗначенияПараметров["-useproxy"] <> Неопределено,Истина,Ложь));
	СтруктураПрокси.Вставить("ИспользоватьАутификациюОС",?(ЗначенияПараметров["-useosauth"] <> Неопределено,Истина,Ложь));
	СтруктураПрокси.Вставить("Сервер",?(ЗначенияПараметров["-proxy-s"] <> Неопределено,ЗначенияПараметров["-proxy-s"] ,""));

	Возврат СтруктураПрокси;
КонецФункции	


//------------

Лог = Логирование.ПолучитьЛог("oscript.app.opm");